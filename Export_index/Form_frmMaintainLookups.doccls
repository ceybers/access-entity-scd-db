
'@Folder "Forms"
Option Compare Database
Option Explicit

Public Enum LogType
    Noop = 0
    Insert = 1
    Update = 2
    Delete = 3
End Enum

Const FORM_TITLE_CAPTION As String = "Lookup Table Maintenance: "
Private ActiveTableName As String
Private ActiveFieldName As String
Private IsEditing As Boolean

' Buttons
Private Sub btnCancel_Click()
    Me.Undo
End Sub

Private Sub btnDelete_Click()
    SetTitleColor True
    
    If MsgBox("Are you sure you want to permanently delete this record?", vbYesNo + vbDefaultButton2 + vbExclamation, "Maintain Lookup Tables") = vbYes Then
        DoCmd.SetWarnings False
        DoCmd.RunCommand acCmdDeleteRecord
        DoCmd.SetWarnings True
        'UpdateButtons
        StopEditing
    End If
    ' TODO Change this to INACTIVE flag instead of deleting
    SetTitleColor False
End Sub

Private Sub btnTest_Click()
    MsgBox "btnTest_Click()"
End Sub

Private Sub btnEdit_Click()
    StartEditing
    Me.txtFieldName.SetFocus
    Me.btnUpdate.Default = True
End Sub

Private Sub btnInsert_Click()
    DoCmd.RunCommand acCmdSaveRecord
End Sub

Private Sub btnUpdate_Click()
    DoCmd.RunCommand acCmdSaveRecord
End Sub

Private Sub btnNew_Click()
    Me.AllowAdditions = True
    DoCmd.GoToRecord record:=acNewRec
    StartEditing
    Me.btnInsert.Default = True
    Me.txtFieldName.SetFocus
End Sub

Private Function UpdateButtons()
    Me.btnNew.Enabled = (Not Me.NewRecord) And (ActiveTableName <> vbNullString) And (Not IsEditing)
    Me.btnEdit.Enabled = (Not IsEditing) And (ActiveTableName <> vbNullString)
    Me.btnUpdate.Enabled = False
    Me.btnInsert.Enabled = False
    Me.btnDelete.Enabled = (Not Me.NewRecord) And (IsEditing)
    Me.btnCancel.Enabled = Me.Dirty Or Me.NewRecord Or IsEditing
End Function

Private Sub Form_Undo(Cancel As Integer)
    StopEditing
End Sub

Private Sub txtFieldName_Change()
    If ActiveFieldName <> vbNullString Then
        If IsEditing And (Not Me.NewRecord) Then
            Me.btnUpdate.Enabled = Me!txtFieldName.Value <> Me.txtFieldName.text
        Else
            Me.btnInsert.Enabled = Me!txtFieldName.text <> vbNullString
        End If
    End If
End Sub

Private Sub Form_AfterDelConfirm(Status As Integer)
    LogEvent Delete
End Sub

Private Sub Form_AfterUpdate()
    StopEditing
End Sub

Private Sub Form_BeforeUpdate(Cancel As Integer)
    If Me.NewRecord Then
        LogEvent Insert
    Else
        LogEvent Update
    End If
End Sub

Private Sub Form_Current()
    UpdateButtons
    SelectCurrentInListBox
    Me.btnEdit.Default = True
    Me.lstHistory.RowSource = "SELECT * FROM logMaintainLookups WHERE TableName = '" & ActiveTableName & "' AND LookupFK = " & Me.txtID & ";"
End Sub

Private Sub Form_Load()
    'Debug.Print "Form_Load()"
    LoadTablesToListbox
    ClearListBox Me.lstRecords
    Me.txtID.ControlSource = vbNullString
    Me.txtFieldName.ControlSource = vbNullString
    Set Me.Recordset = Nothing
    SetFormTitle
    StopEditing
End Sub

Private Function LoadTablesToListbox()
    ClearListBox Me.lstTables
    
    Dim db As DAO.Database
    Dim tdf As DAO.TableDef
    Set db = CurrentDb
    For Each tdf In db.TableDefs
        If (tdf.name Like "lkp*") Then
            Me.lstTables.AddItem tdf.name
        End If
    Next
    Set tdf = Nothing
    Set db = Nothing
End Function

Private Function ClearListBox(lb As ListBox)
    If lb Is Nothing Then Exit Function
    If lb.RowSourceType <> "Value List" Then
        lb.RowSource = vbNullString
        Exit Function
    End If
    Dim i As Integer
    For i = lb.ListCount - 1 To 0 Step -1
        lb.RemoveItem i
    Next i
End Function

Private Sub lstRecords_Click()
    If Me.lstRecords.ListCount <= 1 Then Exit Sub
    If Me.lstRecords = "#DELETED" Then
        LoadItemsFromTableToListbox
        Exit Sub
    End If
    
    Dim idx As Integer
    idx = CInt(Me.lstRecords)
    Dim rs As Recordset
    Set rs = Me.Recordset.Clone
    rs.FindFirst "[ID] = " & idx
    Me.Bookmark = rs.Bookmark

    SelectCurrentInListBox
End Sub

Private Sub lstTables_Click()
    ActiveTableName = CStr(Me.lstTables)
    LoadItemsFromTableToListbox
    RebindForm
    SetFormTitle
End Sub

Private Function LoadItemsFromTableToListbox()
    If ActiveTableName = vbNullString Then Exit Function
    Me.lstRecords.RowSource = "SELECT * FROM " & ActiveTableName
End Function

Private Function RebindForm()
    ActiveFieldName = CurrentDb.OpenRecordset(ActiveTableName).fields(1).name
    
    Me.recordSource = ActiveTableName
    Me.txtID.ControlSource = "ID"
    Me.txtFieldName.ControlSource = ActiveFieldName
    Me.lblFieldName.caption = ActiveFieldName
End Function

Private Function SelectCurrentInListBox()
    If IsNull(Me.txtID) Then Exit Function
    Me.lstRecords = CInt(Me.txtID)
End Function

Private Function SetTitleColor(Optional IsEditing As Boolean = True)
    Me.FormHeader.BackColor = IIf(IsEditing, 4686591, 11829830)
End Function

Private Function SetFormTitle()
    Me.lblTitle.caption = FORM_TITLE_CAPTION & "'" & ActiveTableName & "'"
End Function

Private Function LogEvent(lt As LogType)
    Dim sql As String
    sql = "INSERT INTO logMaintainLookups (TableName,Operation,LookupFK,OldValue,NewValue,[Timestamp]) VALUES ('"
    DoCmd.SetWarnings False
    Select Case lt
        Case Insert
            'Debug.Print "INSERT " & CStr(Me.RecordSource) & " ID#" & CStr(Me.txtID) & " '" & CStr(Me.txtFieldName) & "' at " & Format(Now(), "yyyy/mm/dd hh:MM:ss")
            sql = sql & ActiveTableName & "','INSERT'," & Me.txtID & ",'','" & Me.txtFieldName & "',#" & format(Now(), "MM/dd/yyyy hh:mm:ss") & "#)"
            'Debug.Print sql
            DoCmd.RunSQL sql
        Case Update
            'Debug.Print "UPDATE " & CStr(Me.RecordSource) & " ID#" & CStr(Me.txtID) & " '" & CStr(Me.txtFieldName.OldValue) & "' -> '" & CStr(Me.txtFieldName) & "' at " & Format(Now(), "yyyy/mm/dd hh:MM:ss")
            sql = sql & ActiveTableName & "','UPDATE'," & Me.txtID & ",'" & Me.txtFieldName.OldValue & "','" & Me.txtFieldName & "',#" & format(Now(), "MM/dd/yyyy hh:mm:ss") & "#)"
            'Debug.Print sql
            DoCmd.RunSQL sql
        Case Delete
            'Debug.Print "DELETE " & CStr(Me.RecordSource) & " ID#" & CStr(Me.txtID) & " '" & CStr(Me.txtFieldName) & "' at " & Format(Now(), "yyyy/mm/dd hh:MM:ss")
            sql = sql & ActiveTableName & "','DELETE'," & Me.txtID & ",'" & Me.txtFieldName & "','',#" & format(Now(), "MM/dd/yyyy hh:mm:ss") & "#)"
            'Debug.Print sql
            DoCmd.RunSQL sql
        Case Else
            'Debug.Print "LogEvent ELSE"
    End Select
    DoCmd.SetWarnings True
End Function

Private Function StartEditing()
    IsEditing = True
    'Debug.Print "Start Editing"
    SetTitleColor True
    Me.txtFieldName.Locked = False
    Me.lstRecords.Locked = True
    UpdateButtons
End Function

Private Function StopEditing()
    IsEditing = False
    Me.lstRecords.Requery
    SelectCurrentInListBox
    'Debug.Print "Stop editing"
    SetTitleColor False
    Me.txtFieldName.Locked = True
    Me.lstRecords.Locked = False
    Me.AllowAdditions = False
    UpdateButtons
End Function