'@Folder "Subforms"
Option Compare Database
Option Explicit

Private WithEvents detailModel As clsDetailViewModel

' Properties
Public Property Get ViewModel() As clsDetailViewModel
    If detailModel Is Nothing Then
        Set detailModel = New clsDetailViewModel
    End If
    Set ViewModel = detailModel
End Property

Private Sub detailModel_StateChanged(newState As DetailStateForSCD)
    If newState = Viewnew Then
        Me.btnDetailAdd.Enabled = True
        Me.btnDetailAdd.SetFocus
        Me.sfrmDetail.Visible = False
    Else
        Me.sfrmDetail.Visible = True
    End If
End Sub

' Ctor
Private Sub Form_Load()
    Me.sfrmDetail.SourceObject = vbNullString
    Set ViewModel.SubForm = Me.sfrmDetail
    MapButtonsToStates ViewModel
End Sub

' Private Methods
Private Sub MapButtonsToStates(vm As clsDetailViewModel)
    With vm
        .AddEvents Me.btnDetailAdd, DetailStateForSCD.EditNew
        .AddEvents Me.btnDetailCancel, DetailStateForSCD.CancelAny
        .AddEvents Me.btnDetailEdit, DetailStateForSCD.EditExisting
        .AddEvents Me.btnDetailSave, DetailStateForSCD.SaveAny
    End With
End Sub

Private Sub DisableCommandButtons()
    Dim ctl As control
    For Each ctl In Me.controls
        If ctl.name Like "btn*" Then
            ctl.Enabled = False
        End If
    Next ctl
End Sub