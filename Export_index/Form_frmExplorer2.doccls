'@Folder "Explorer2"
Option Compare Database
Option Explicit

' Variables
Private WithEvents ViewModel As ViewModelExplorer2
Private Const ENTITY_PREFIX As String = "Entity#"
Private Const ENTITYTYPE_PREFIX As String = "EntityType#"
Private Const DETAIL_TABLENAME_COLUMN As Long = 2

' Faux-static Getter for VM
Private Property Get GetVM() As ViewModelExplorer2
    If ViewModel Is Nothing Then
        Set ViewModel = New ViewModelExplorer2
        MapEventButtons ViewModel
        Set ViewModel.Subform = Me.chldDetailSubform
    End If
    Set GetVM = ViewModel
End Property

' Control Events
Private Sub cboEntityType_Change()
    If IsNull(Me.cboEntityType.Column(0)) Then
        Exit Sub
    End If
    GetVM.entityTypeid = Me.cboEntityType.Column(0)
    DisplayEntityType
End Sub

Private Sub cmbSearch_Click()
    If IsNull(Me.txtSearch) Then
        Exit Sub
    End If
    
    Me.txtSearch.SetFocus
    GetVM.SearchCriteria = Me.txtSearch.text
    SearchEntity
End Sub


Private Sub Form_Close()
    Dim tv As MSComctlLib.treeview
    If Me.tvEntities Is Nothing Then Exit Sub
    If Me.tvEntities.Object Is Nothing Then Exit Sub
    Set tv = Me.tvEntities.Object
    'Set lv = Me.lvDetails.Object
    Explorer2TreeView.ClearTreeview tv
    'lv.ListItems.Clear
End Sub

Private Sub Form_Load()
    Me.chldDetailSubform.SourceObject = vbNullString
    InitializeImageList Me.ilImageListLarge.Object, 32, 32
    InitializeImageList Me.ilImageListSmall.Object, 16, 16
    
    PopulateListView Me.lvDetails.Object, Me.ilImageListLarge.Object, Me.ilImageListLarge.Object

    Me.cboEntityType = Me.cboEntityType.Column(0, 3)
    cboEntityType_Change
    'PopulateTreeview 4
End Sub

Private Sub MapEventButtons(vm As ViewModelExplorer2)
    With vm
        .AddEvents Me.cmbAdd, "EditNew"
        .AddEvents Me.cmbEdit, "EditExisting"
        .AddEvents Me.cmbSave, "Save"
        .AddEvents Me.cmbSearch, "Search"
        .AddEvents Me.cmbViewMany, "ViewMany"
        .AddEvents Me.cmbViewOne, "ViewOne"
    End With
End Sub


Private Sub lvDetails_DblClick()
    Dim li As ListItem
    Dim lv As listview
    
    Set lv = Me.lvDetails.Object
    Set li = lv.SelectedItem
    
    If li Is Nothing Then Exit Sub

    GetVM.DetailTableName = li.key
    DisplayDetail
End Sub

Private Sub tvEntities_Collapse(ByVal Node As Object)
    'On Error Resume Next ' TODO FIX
    Exit Sub
    Dim nde As Node
    Set nde = Node
    If nde.Tag <> "EntityTypeID#4" Then
        nde.Image = "Kdirectory_closed"
    End If
End Sub

Private Sub tvEntities_Expand(ByVal Node As Object)
    'On Error Resume Next ' TODO FIX
    Exit Sub
    Dim nde As Node
    Set nde = Node
    If nde.Tag <> "EntityTypeID#4" Then
        nde.Image = "Kdirectory_open"
    End If
End Sub

Private Sub tvEntities_NodeClick(ByVal Node As Object)
    Debug.Assert Not Node Is Nothing
    
    GetVM.EntityID = Replace(Node.key, ENTITY_PREFIX, vbNullString)
    DisplayEntity
End Sub

Private Sub txtSearch_KeyUp(KeyCode As Integer, Shift As Integer)
    Exit Sub
    If IsNull(Me.txtSearch) Then
        Exit Sub
    End If
    
    GetVM.SearchCriteria = Me.txtSearch.text
    SearchEntity
End Sub

' Methods
Private Sub DisplayEntity()
    Me.RecordsetClone.FindFirst "[ID] = " & GetVM.EntityID
    Me.Bookmark = Me.RecordsetClone.Bookmark
    UpdateCaption
End Sub

Private Sub SearchEntity()
    Dim criteria As String
    Dim nde As Node
    
    criteria = GetVM.SearchCriteria
    If Len(criteria) < 3 Then Exit Sub
    
    Set nde = FindNodeInTreeview(Me.tvEntities.Object, criteria)
    If Not nde Is Nothing Then
        nde.selected = True
        nde.EnsureVisible
        GetVM.EntityID = Replace(nde.key, ENTITY_PREFIX, vbNullString)
        DisplayEntity
    End If
End Sub

Private Sub DisplayEntityType()
    PopulateTreeview GetVM.entityTypeid
End Sub

Private Sub DisplayDetail()
    Me.chldDetailSubform.SourceObject = GetVM.DetailSubFormName
    UpdateCaption
End Sub

Private Sub PopulateTreeview(ByVal entityTypeid As Double)
    Dim vm As ViewModelExplorer2
    Dim tv As MSComctlLib.treeview
    
    Set tv = Me.tvEntities.Object
    Set vm = GetVM
    
    Me.recordSource = vm.GetSQL
        
    InitializeTreeview tv, Me.ilImageListSmall.Object
    ClearTreeview tv
    PopulateTreeviewFromCollection tv, GetVM.GetCollectionFromRecordset
End Sub

Private Sub UpdateCaption()
    Dim newcaption As String
    newcaption = GetVM().DetailTableName & " @ Entity#" & GetVM().EntityID
    Me.lblSubformCaption.Caption = newcaption
End Sub

Private Sub ViewModel_CancelEditing()
    SetControlState True
End Sub

Private Sub ViewModel_StartEditing()
    SetControlState False
End Sub

Private Sub ViewModel_StopEditing()
    SetControlState True
End Sub

Private Sub SetControlState(ByVal enable As Boolean)
    'Me.tvEntities.Locked = enable
    'Me.lvDetails.Locked = enable
    Me.cmbAdd.Enabled = enable
    Me.cmbEdit.Enabled = enable
    Me.cmbSave.Enabled = Not enable
    Me.cmbViewMany.Enabled = enable
    Me.cmbViewOne.Enabled = enable
End Sub