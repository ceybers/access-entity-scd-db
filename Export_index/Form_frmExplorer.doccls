'@Folder "Forms"
Option Compare Database
Option Explicit

Private Const COMMIT_FORMNAME As String = "sfrmCommits"
Private Const DETAIL_FORMNAME As String = "sfrmDetails"
Private Const ENTITY_FORMNAME As String = "sfrmEntities"

Private WithEvents commitModel As clsCommitViewModel
Private WithEvents entityModel As clsEntityViewModel
Private WithEvents detailModel As clsDetailViewModel

' VM Events
Private Sub commitModel_CommitChanged()
    TryUpdateDetail
End Sub

Private Sub entityModel_DetailChanged()
    TryUpdateDetail
End Sub

Private Sub entityModel_EntityChanged()
    TryUpdateDetail
End Sub

Private Sub detailModel_EditingStarted()
    Me.sfrmEntity.Locked = True
    Me.sfrmCommit.Locked = True
End Sub

Private Sub detailModel_EditingStopped()
    Me.sfrmEntity.Locked = False
    Me.sfrmCommit.Locked = False
End Sub

' Ctor
Private Sub Form_Load()
    InitializeCommits
    ResetFormLayout
End Sub

' Private Methods
Private Sub InitializeCommits()
    Dim commitSubform As Form_sfrmCommits
    Dim entitySubform As Form_sfrmEntities
    Dim detailSubform As Form_sfrmDetails
    
    Me.sfrmDetail.SourceObject = DETAIL_FORMNAME
    Set detailSubform = Me.sfrmDetail.Form
    Set detailModel = detailSubform.ViewModel
    
    Me.sfrmCommit.SourceObject = COMMIT_FORMNAME
    Set commitSubform = Me.sfrmCommit.Form
    Set commitModel = commitSubform.ViewModel
    
    Me.sfrmEntity.SourceObject = ENTITY_FORMNAME
    Set entitySubform = Me.sfrmEntity.Form
    Set entityModel = entitySubform.ViewModel
    
    TryUpdateDetail
End Sub

Private Sub TryUpdateDetail()
    If Not commitModel.IsValid Then Exit Sub
    If Not entityModel.IsValid Then Exit Sub
    
    detailModel.entityID = entityModel.Entity.ID
    Set detailModel.Detail = entityModel.Detail
    detailModel.CommitID = commitModel.Commit.ID
End Sub

Private Sub ResetFormLayout()
    Me.sfrmEntity.width = 13.7 * 567
    Me.sfrmCommit.width = 16 * 567
    Me.sfrmCommit.Height = 2 * 567
    Me.sfrmDetail.Height = 16 * 567
    
    Me.sfrmEntity.Locked = False
    Me.sfrmCommit.Locked = False
End Sub